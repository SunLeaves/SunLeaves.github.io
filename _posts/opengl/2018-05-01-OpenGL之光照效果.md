---
layout:     post
title:      OpenGL之光照效果实现
subtitle:   
date:       2018-05-01
author:     xiezht
header-img: 
catalog: true
tags: 
    - OpenGL
---

* 参考文档：[LearnOpenGL CN](https://learnopengl-cn.github.io/)

* 源码实现地址：[Shadow Mapping Codes](https://github.com/xiezht/Computer-Graphics-02/tree/master/OpenGL-Test-06)

# OpenGL之光照效果实现

## 运行结果：

![图片](https://images-cdn.shimo.im/zh9NN1RIHIgc9cnP/image.png!thumbnail)

## 实验思路：

* 两种Shading方法在实际操作中的区别在于：
  * Phong Shading在片段着色器部分
    * 根据由三个顶点线性插值插值得到的法向量计算出像素的法向量
    * 再根据光照方向、视角等计算出漫反射、镜面反射光照分量
    * 最后计算出每个像素的颜色值
  * Gouraud Shading在顶点着色器部分
    * 用相同的方法计算出每个顶点的颜色值
    * 图元内部的像素值，由光栅化过程，使用三个顶点的颜色值插值得到。
* Phong Shading
  * 顶点着色器部分
    * 输出顶点的位置 fragPos，法向量 normal
  * 片段着色器部分
    * 光栅化过程将根据传入的顶点位置fragPos、法向量normal，计算出三角形图元内每个像素的位置、法向量，作为传入片段着色器的fragPos，normal
    * 片段着色器利用Uniform变量设置视角位置，光照颜色，光照位置，物体颜色以及环境光、镜面反射、漫反射、反光度的参数因子
    * 根据光照分量公式计算出ambient、diffuse、specular三个光照分量，相加后物体颜色相乘，等到最终的片段颜色
  * 代码如下：
    * 顶点着色器代码：

```
#version 330 core

layout (location = 0) in vec3 aPos;
layout (location = 1) in vec3 aNormal;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

out vec3 fragPos;
out vec3 normal;

void main()
{
	gl_Position = projection * view * model * vec4(aPos, 1.0f);
	fragPos = vec3(model * vec4(aPos, 1.0f));
	normal = mat3(transpose(inverse(model))) * aNormal;
}
```

* 片段着色器代码：

```
#version 330 core

uniform vec3 viewPos;
uniform vec3 lightPos;
uniform vec3 lightColor;
uniform vec3 objColor;

uniform float ambientFactor;
uniform float specularFactor;
uniform float diffuseFactor;
uniform int shininess;

in vec3 fragPos;
in vec3 normal;

out vec4 fragColor;

void main()
{
	vec3 ambient = lightColor;

	vec3 stdNormal = normalize(normal);
	vec3 lightDir = normalize(lightPos-fragPos);
	float diff = max(dot(stdNormal, lightDir), 0.0f);
	vec3 diffuse = diff * lightColor;

	vec3 viewDir = normalize(viewPos - fragPos);
	vec3 reflectDir = reflect(-lightDir, stdNormal);
	float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
	vec3 specular = spec * lightColor;

	vec3 result = ( ambientFactor*ambient+diffuseFactor*diffuse+specularFactor*specular) * objColor;

	fragColor = vec4(result, 1.0f);
}

```

* Gouraud Shading
  * 顶点着色器部分
    * 与Phong Shading的顶点着色器部分类似，但操作的是三角形的三个顶点
    * 根据顶点的法向量、传入的Uniform变量：物体位置、光源位置、光源颜色、物体颜色以及其他光照分量的参数因子，根据公式计算出光照分量ambient、diffuse、shiness即可
    * 参数因子分别乘以光照分量，相加后，与物体颜色相乘即可得到顶点颜色
    * 将顶点颜色作为片段着色器的输入量：outColor
  * 片段着色器
    * 将顶点着色器的输出量：outColor作为自身的输入量
    * 光栅化过程将根据三个顶点的outColor插值计算出图元内部像素的颜色
    * 因此直接将传入的outColor作为片段着色器的输出，即每个像素的颜色。
  * 着色器源代码：
    * 顶点着色器：

```
#version 330 core
layout (location = 0) in vec3 aPos;
layout (location = 1) in vec3 aNormal;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

uniform vec3 viewPos;
uniform vec3 lightPos;
uniform vec3 lightColor;
uniform vec3 objColor;

uniform float ambientFactor;
uniform float specularFactor;
uniform float diffuseFactor;
uniform int shininess;

out vec3 outColor;

void main()
{
	gl_Position = projection * view * model * vec4(aPos, 1.0f);

	vec3 fragPos = vec3(model * vec4(aPos, 1.0f));
	vec3 normal = mat3(transpose(inverse(model))) * aNormal;
	vec3 ambient = ambientFactor * lightColor;

	vec3 stdNormal = normalize(normal);
	vec3 lightDir = normalize(lightPos-fragPos);
	float diff = max(dot(stdNormal, lightDir), 0.0f);
	vec3 diffuse = diffuseFactor * diff * lightColor;

	vec3 viewDir = normalize(viewPos - fragPos);
	vec3 reflectDir = reflect(-lightDir, stdNormal);
	float spec = pow(max(dot(viewDir, reflectDir), 0.0), 256);
	vec3 specular = specularFactor * spec * lightColor;

	outColor = (ambient+diffuse+specular) * objColor;
}

```

* 片段着色器：

```
#version 330 core

in vec3 outColor;

out vec4 FragColor;

void main()
{
	FragColor = vec4(outColor, 1.0f);
}

```

